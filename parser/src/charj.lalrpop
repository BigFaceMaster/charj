use std::str::FromStr;
use num_bigint::BigInt;
use num_bigint::BigUint;
use num_traits::Pow;
use std::ops::Mul;

use crate::location::{Loc, Location};
use crate::lexer;
use crate::token::{Token, CommentType};
use crate::error::LexicalError;
use crate::parse_tree::*;

grammar<'input>(input: &'input str, file_no: usize);

pub Charj: SourceUnit = {
    SourceUnitPart+ => SourceUnit(<>)
};

SourceUnitPart: SourceUnitPart = {
    PackageDirective => SourceUnitPart::PackageDirective(<>),
    ImportDirective => SourceUnitPart::ImportDirective(<>),
    StructDef => SourceUnitPart::StructDef(<>),
    FuncDef => SourceUnitPart::FuncDef(<>),
    StructFuncDef => SourceUnitPart::StructFuncDef(<>),
}

PackageDirective: Package = {
   "package" <s:Identifier> => Package::Plain(s),
   "pkg" <s:Identifier> => Package::Plain(s),
}

ImportDirective: Import = {
    "import" <s:Identifier> => Import::Standard(s),
    "import" <s:StringLiteral> "as" <id:Identifier> ";" => Import::GlobalSymbol(s, id)
}

StructDef: Box<StructDef> = {
     <l:@L> "struct" <name:Identifier> "{" <fields:(<VarDecl>)*> "}" <r:@R> => {
        Box::new(StructDef{loc: Loc(l, r), name, fields})
    }
}

FuncDef: Box<FuncDef> = {
    <l:@L> "fun" <name:Identifier> <params:ParameterList?> "{" <body:Suite?> "}" <r:@R> => {
        let params = params.unwrap_or(Vec::new());
        let body = body.unwrap_or(Vec::new());

        Box::new(FuncDef {
            loc: Loc(l, r),
            name,
            params,
            body: body,
       })
    }
};

StructFuncDef: Box<StructFuncDef> = {
    <l:@L> <struct_name:Identifier> "$" <name:Identifier> <params:ParameterList?> "{" <body:Suite?> "}" <r:@R> => {
        let params = params.unwrap_or(Vec::new());
        let body = body.unwrap_or(Vec::new());

        Box::new(StructFuncDef{
            loc: Loc(l, r),
            name, struct_name,
            params,
            body,
        })
    }
}

VarDecl: Statement = {
    <l:@L><field:Identifier> ":" <ty:TypeLiteral><r:@R> => {
        Statement {
            location: Location::new(l, r),
            node: StatementType::Variable {
                field,
                ty
            }
        }
    }
};

Suite: Suite = {
    <s:Statement+> => s.into_iter().flatten().collect(),
};

Statement: Suite = {
    <s:CompoundStatement> => vec![s],
};

CompoundStatement: Statement = {
    IfStatement,
    WhileStatement,
    ForStatement,
    FlowStatement,
    ExpressionStatement,
}

ExpressionStatement: Statement = {
    <l:@L> "let" <name:Identifier> ":" <typ:TypeLiteral> "=" <e:Expression> <r:@R> => {
        Statement {
            location: Location::new(l, r),
            node: StatementType::Assign {
                target: name,
                ty: typ,
                value: e
            },
        }
    },
    <l:@L> <e:Expression> <r:@R> => {
        Statement {
            location: Location::new(l, r),
            node: StatementType::Expression { expression: e },
        }
    }
}

ExpressionNoCond: Expression = Comparison;

AndTest: Expression = {
    <e1:Comparison> <l:@L> <e2:("&&" Comparison)*> <r:@R> => {
        if e2.is_empty() {
            e1
        } else {
            let mut values = vec![e1];
            values.extend(e2.into_iter().map(|e| e.1));
            Expression {
                location: Location::new(l, r),
                node: ExpressionType::BoolOp { op: BooleanOperator::And, values }
            }
        }
    },
}

Comparison: Expression = {
    <e:Expression> <l:@L> <comparisons:(CompOp Expression)+> <r:@R> => {
        let mut vals = vec![e];
        let mut ops = vec![];
        for x in comparisons {
            ops.push(x.0);
            vals.push(x.1);
        }
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::Compare { vals, ops }
        }
    },
    Expression,
};

CompOp: Comparison = {
    "==" => Comparison::Equal,
    "!=" => Comparison::NotEqual,
    "<" => Comparison::Less,
    "<=" => Comparison::LessOrEqual,
    ">" => Comparison::Greater,
    ">=" => Comparison::GreaterOrEqual,
};

FlowStatement: Statement = {
    <l:@L> "break" <r:@R> => {
        Statement {
            location: Location::new(l, r),
            node: StatementType::Break,
        }
    },
    <l:@L> "continue" <r:@R> => {
        Statement {
            location: Location::new(l, r),
            node: StatementType::Continue,
        }
    },
    <l:@L> "return" <value:ReturnList?> ";" <r:@R> => {
        Statement {
            location: Location::new(l, r),
            node: StatementType::Return { value },
        }
    },
}

ReturnList: Expression = {
    <l:@L> <elements:OneOrMore<ReturnValue>> <trailing_comma: ","?> <r:@R> => {
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::List { elements },
        }
    }
}

ReturnValue: Expression = {
    <l:@L><n:Identifier><r:@R> => {
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::Identifier { name: n }
        }
    }
}

IfStatement: Statement = {
    <l:@L> "if" "(" <cond:ExpressionNoCond> ")" "{" <body:Suite?> "}" <s3:("else" "{" Suite "}")?> <r:@R> => {
        let mut last = s3.map(|s| s.2);

        let body = body.unwrap_or(Vec::new());

        Statement {
            location: Location::new(l, r),
            node: StatementType::If {
                cond,
                body,
                orelse: last
            }
        }
    }
};

WhileStatement: Statement = {
    <l:@L> "while" "(" <cond:ExpressionNoCond> ")" "{" <body:Suite?> "}" <r:@R> => {
        let body = body.unwrap_or(Vec::new());
        Statement {
            location: Location::new(l, r),
            node: StatementType::While {
                cond,
                body
            }
        }
    }
};

ForStatement: Statement = {
    // todo: change target to ExpressionList,
    <l:@L> "for" "(" <target:Expression> "in" <iter:Expression> ")" "{" <body:Suite?> "}" <r:@R> => {
        let body = body.unwrap_or(Vec::new());

        Statement {
            location: Location::new(l, r),
            node: StatementType::For {
                target: Box::new(target),
                iter: Box::new(iter),
                body
            }
        }
    }
};

Expression: Expression = {
    UnaryExpr,
}

UnaryExpr: Expression = {
    RangeExpression
}

RangeExpression: Expression = {
    <e1:RangeExpression> <l:@L> ".." <e2:OrExpression> <r:@R> => {
        Expression {
           location: Location::new(l, r),
           node: ExpressionType::Range { start: Box::new(e1), end: Box::new(e2) }
       }
    },
    OrExpression
}

OrExpression: Expression = {
    <e1:OrExpression> <l:@L> "||" <e2:AndExpression> <r:@R> => {
       let mut values = vec![e1];
       values.push(e2);

       Expression {
           location: Location::new(l, r),
           node: ExpressionType::BoolOp { op: BooleanOperator::Or, values }
       }
   },
    AndExpression
}

AndExpression: Expression = {
    <e1:AndExpression> <l:@L> "&&" <e2:ArithmaticExpression> <r:@R> => {
        let mut values = vec![e1];
        values.push(e2);

        Expression {
            location: Location::new(l, r),
            node: ExpressionType::BoolOp { op: BooleanOperator::And, values }
        }
    },
    ArithmaticExpression
}

// Unary Expr
ArithmaticExpression: Expression = {
    <a:ArithmaticExpression> <l:@L> <op:AddOp> <b:Term> <r:@R> => Expression {
        location: Location::new(l, r),
        node: ExpressionType::Binop { a: Box::new(a), op, b: Box::new(b) }
    },
    Term
};

AddOp: Operator = {
    "+" => Operator::Add,
    "-" => Operator::Sub,
};

Term: Expression = {
    <a:Term> <l:@L> <op:MulOp> <b:PrimaryExpr> <r:@R> => Expression {
        location: Location::new(l, r),
        node: ExpressionType::Binop { a: Box::new(a), op, b: Box::new(b) }
    },
    PrimaryExpr,
};

PrimaryExpr: Expression = {
    Atom,
    <f:PrimaryExpr> <l:@L> "(" <a: Comma<Argument>> ")" <r:@R> => {
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::Call { function: Box::new(f), args: a }
        }
    },
    <e:PrimaryExpr> <l:@L> "." <name:Identifier> <r:@R> => {
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::Attribute { value: Box::new(e), name }
        }
    },
}

UnOp: UnaryOperator = {
    "+" => UnaryOperator::Pos,
    "-" => UnaryOperator::Neg,
    "~" => UnaryOperator::Inv,
};

MulOp: Operator = {
    "*" => Operator::Mult,
    "/" => Operator::Div,
    "%" => Operator::Mod,
};

Atom: Expression = {
    // system type & user custom type
    TypeLiteral,
    // "string"
    <l:@L> <value:LexStringLiteral> <r:@R> => Expression {
        location: Location::new(l, r),
        node: ExpressionType::String { value: value.to_string() }
    },
    <l:@L> <n:LexNumber> <r:@R> => {
        let base: String = n.0.chars().filter(|v| *v != '_').collect();
        let exp: String = n.1.chars().filter(|v| *v != '_').collect();

        let n = if exp.is_empty() {
            BigInt::from_str(&base).unwrap()
        } else {
            let base = BigInt::from_str(&base).unwrap();
            let exp = BigInt::from_str("10").unwrap().pow(BigUint::from_str(&exp).unwrap());

            base.mul(exp)
        };

        Expression {
            location: Location::new(l, r),
            node: ExpressionType::Number { value: n }
        }
    },
}

TypeLiteral: Expression = {
    // bool, int, string
    <l:@L> <ty:Type> <r:@R> => {
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::Type {ty}
        }
    },
    // name
    <l:@L> <name: Identifier> <r:@R> => {
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::Identifier { name }
        }
    },
    // list
    <l:@L> "[" "]" <ty: TypeLiteral><r:@R> => {
        Expression {
            location: Location::new(l, r),
            node: ExpressionType::List { elements: vec![] },
        }
    },
}

//ArgumentList: ArgumentList = {
//    <Comma<Argument>> => <>,
//};

Type: Type = {
    "bool" => Type::Bool,
    "string" => Type::String,
    Int => Type::Int(<>),
}

Argument: Argument = {
    <l:@L> <p:Expression> <r:@R> =>  {
        Argument { location: Location::new(l, r), expr: p }
    },
}

ParameterList: Vec<(Loc, Option<Parameter>)> = {
    "(" ")" => Vec::new(),
    "(" <l:@L> <p:Parameter> <r:@R> ")" => vec!((Loc(l, r), Some(p))),
    "(" <CommaTwo<OptParameter>> ")" => <>,
}

OptParameter: (Loc, Option<Parameter>) = {
    <l:@L> <p:Parameter?> <r:@R> => (Loc(l, r), p),
}

// A parameter list is used for function arguments, returns, and destructuring statements.
// In destructuring statements, parameters can be optional. So, we make parameters optional
// and as an added bonus we can generate error messages about missing parameters/returns
// to functions
Parameter: Parameter = {
    <l:@L> <ty:Expression> <name:Identifier?> <r:@R> => {
        let loc = Loc(l, r);
        Parameter{loc, ty, name}
    }
}

Identifier: Identifier = {
    <l:@L> <n:LexIdentifier> <r:@R> => Identifier{loc: Loc(l, r), name: n.to_string()}
}

StringLiteral: StringLiteral = {
    <l:@L> <s:LexStringLiteral> <r:@R> => {
        StringLiteral{ loc: Loc(l, r), string: s.to_string() }
    }
}

#[inline]
Comma<T>: Vec<T> = {
    <items: (<T> ",")*> <last: T?> => {
        let mut items = items;
        items.extend(last);
        items
    }
};
//
//CommaOne<T>: Vec<T> = {
//    <e:T> <v:("," <T>)*>  => {
//        let mut v = v;
//        v.insert(0, e);
//        v
//    }
//};

#[inline]
OneOrMore<T>: Vec<T> = {
    <i1: T> <i2:("," T)*> => {
        let mut items = vec![i1];
        items.extend(i2.into_iter().map(|e| e.1));
        items
    }
};

CommaTwo<T>: Vec<T> = {
    <e:T> <v:("," <T>)+>  => {
        let mut v = v;
        v.insert(0, e);
        v
    }
};

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token<'input> {
        LexIdentifier => Token::Identifier(<&'input str>),
        LexStringLiteral => Token::StringLiteral(<&'input str>),
        LexNumber => Token::NumberLiteral(<&'input str>, <&'input str>),

        DocComment => Token::DocComment(<CommentType>, <&'input str>),
        // operators symbol

        // keywords
        "default" => Token::Default,
        "import" => Token::Import,
        "package" => Token::Package,
        "struct" => Token::Struct,
        "pkg" => Token::Package,
        "as" => Token::As,
        "fun" => Token::Fun,
        "let" => Token::Let,

        // statement
        "if" => Token::If,
        "else" => Token::Else,
        "while" => Token::While,
        "for" => Token::For,
        "in" => Token::In,
        ".." => Token::Range,
        "break" => Token::Break,
        "continue" => Token::Continue,
        "return" => Token::Return,

        "bool" => Token::Bool,
        "string" => Token::String,
        "bytes" => Token::DynamicBytes,
        Int => Token::Int(<u16>),
        Uint => Token::Uint(<u16>),
        Bytes => Token::Bytes(<u8>),

        // other symbols
        "\n" => Token::NewLine,
        "$" => Token::Binding,

        ";" => Token::Semicolon,
        "{" => Token::OpenCurlyBrace,
        "}" => Token::CloseCurlyBrace,
        "(" => Token::OpenParenthesis,
        ")" => Token::CloseParenthesis,
        "=" => Token::Assign,
        "==" => Token::Equal,
        "=>" => Token::Arrow,

        "|=" => Token::BitwiseOrAssign,
        "^=" => Token::BitwiseXorAssign,
        "&=" => Token::BitwiseAndAssign,
        "<<=" => Token::ShiftLeftAssign,
        ">>=" => Token::ShiftRightAssign,
        "+=" => Token::AddAssign,
        "-=" => Token::SubtractAssign,
        "*=" => Token::MulAssign,
        "/=" => Token::DivideAssign,
        "%=" => Token::ModuloAssign,

        "?" => Token::Question,
        ":" => Token::Colon,
        "||" => Token::Or,
        "&&" => Token::And,
        "!=" => Token::NotEqual,
        "<" => Token::Less,
        "<=" => Token::LessEqual,
        ">" => Token::More,
        ">=" => Token::MoreEqual,
        "|" => Token::BitwiseOr,
        "&" => Token::BitwiseAnd,
        "^" => Token::BitwiseXor,
        "<<" => Token::ShiftLeft,
        ">>" => Token::ShiftRight,
        "+" => Token::Add,
        "-" => Token::Subtract,
        "*" => Token::Mul,
        "/" => Token::Divide,
        "%" => Token::Modulo,
        "**" => Token::Power,
        "!" => Token::Not,
        "~" => Token::Complement,
        "++" => Token::Increment,
        "--" => Token::Decrement,
        "[" => Token::OpenBracket,
        "]" => Token::CloseBracket,
        "." => Token::Member,
        "," => Token::Comma,
    }
}

